pipeline {
  agent any

  //Configure the following environment variables before executing the Jenkins Job
  environment {
    APIProxyName = "CICDAPIProxy"
    APIPortalHost = "${env.API_HOST}"
    APIPortalOAuthHost = "${env.API_OAUTH_HOST}"
    APIPortalOAuthCredentials = "${env.API_OAUTH_CRED}"
    GITRepositoryURL = "${env.GIT_REPOSITORY_URL}"
    GITCredentials = "${env.GIT_CRED}"
    GITBranch = "${env.GIT_BRANCH_NAME}"
    GITFolder = "IntegrationContent/API"
    GITComment = "API Proxy update from CICD pipeline"
  }

  stages {

    stage('Clone Git Repository to Workspace') {
      steps {
        script {
          //Delete workspace before cloning from Git
          println '**** Delete workspace before cloning from Git ****';
          deleteDir();

          //Checkout from Git
          checkout([
            $class: 'GitSCM',
            branches: [
              [name: env.GITBranch]
            ],
            doGenerateSubmoduleConfigurations: false,
            extensions: [
              [$class: 'RelativeTargetDirectory', relativeTargetDir: "."],
              [$class: 'SparseCheckoutPaths', sparseCheckoutPaths: [
                [$class: 'SparseCheckoutPath', path: "${env.GITFolder}"]
              ]]
            ],
            submoduleCfg: [],
            userRemoteConfigs: [
              [
                credentialsId: env.GITCredentials,
                url: 'https://' + env.GITRepositoryURL
              ]
            ]
          ])
        }
      }
    }

    stage('Generate API Portal OAuth Bearer token') {
      steps {
        script {
          //Generate API Portal OAuth Bearer token
          println '**** Generate API Portal OAuth Bearer token ****';
          try {
            def getTokenResp = httpRequest acceptType: 'APPLICATION_JSON',
              authentication: env.APIPortalOAuthCredentials,
              contentType: 'APPLICATION_JSON',
              httpMode: 'POST',
              ignoreSslErrors: true,
              responseHandle: 'LEAVE_OPEN',
              timeout: 30,
              url: 'https://' + env.APIPortalOAuthHost + '/oauth/token?grant_type=client_credentials'
            def jsonObj = readJSON text: getTokenResp.content
            def token_s1 = 'bearer' + ' ' + jsonObj.access_token
            env.token = token_s1
            getTokenResp.close();
          } catch (Exception e) {
              error("Bearer token generation failed:\n${e}")
          }
        }
      }
    }
    
    stage('Get API Proxy') {
      steps {
        script {
          //Get API Proxy
          println '**** Get API Proxy ****';
          try {
            def APIProxy = httpRequest acceptType: 'APPLICATION_ZIP',
              httpMode: 'GET',
              ignoreSslErrors: true,
              customHeaders: [
                [maskValue: true, name: 'Authorization', value: env.token]
              ],
              timeout: 30,
              url: 'https://' + env.APIPortalHost + '/apiportal/api/1.0/Transport.svc/APIProxies?name=' + env.APIProxyName,
              outputFile: env.GITFolder + '/APIProxy.zip'

            def disposition = APIProxy.headers.toString();
            APIProxy.close();

            //Unzip downloaded API Proxy content
            println '**** Unzip downloaded API Proxy content ****';
            def index = disposition.indexOf('filename') + 9;
            def lastIndex = disposition.indexOf('.zip', index);
            def fileName = disposition.substring(index + 1, lastIndex + 4);
            def folderName = fileName.substring(0, fileName.indexOf('.zip'));
            println 'folder': folderName
            fileOperations([fileUnZipOperation(filePath: env.GITFolder + '/APIProxy.zip', targetLocation: env.GITFolder + '//' + env.APIProxyName)])
            
            //Delete ZIP file from the Workspace
            fileOperations([fileDeleteOperation(excludes: '', includes: env.GITFolder + '\\' + 'APIProxy.zip')])
          } catch (Exception e) {
              error("API Proxy GET call failed:\n${e}")
          }
        }
      }
    }

    stage('Push Downloaded API Proxy to Git') {
      steps {
        script {
          //Push API Proxy to Git
          println '**** Push API Proxy to Git ****';
          dir(env.GITFolder) {
            sh 'git add .'
          }
          withCredentials([
            [$class: 'UsernamePasswordMultiBinding', credentialsId: env.GITCredentials, usernameVariable: 'GIT_AUTHOR_NAME', passwordVariable: 'GIT_PASSWORD']
          ]) {
            sh 'git diff-index --quiet HEAD || git commit -am ' + '\'' + env.GITComment + '\''
            sh('git push https://${GIT_AUTHOR_NAME}:${GIT_PASSWORD}@' + env.GITRepositoryURL + ' HEAD:' + env.GITBranch)
          }
          
          //Delete workspace
          deleteDir()
        }
      }
    }
  }
}
